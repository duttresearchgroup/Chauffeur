#!/usr/bin/gnuplot
reset

set terminal pngcairo enhanced font 'Verdana,8'
set output '../tmp_graphs/ram.png'
set title "RAM usage graph"
set key bmargin

set style line 1 lc rgb '#e74c3c' pt 1 ps 1 lt 1 lw 2 
set style line 2 lc rgb '#3498db' pt 6 ps 1 lt 1 lw 2 
set style line 3 lc rgb '#28b463' pt 4 ps 1 lt 1 lw 2 
set style line 4 lc rgb '#f5b041' pt 4 ps 1 lt 1 lw 2 

set style line 11 lc rgb '#2c3e50' lt 1 lw 1.5
set border 3 back ls 11
set tics nomirror
set autoscale xy
set xdata time
set timefmt "%H:%M:%S"
set format x "%H:%M"
set xlabel "Time"
set ylabel "Memory (MB)"

set style line 11 lc rgb '#aeb6bf' lt 0 lw 2
set grid back ls 11

# Real used RAM = Used - cached - buffers - free
plot '../tmp_data/ram.dat' using 1:($2/1024+$5/1024+$6/1024) title 'Free' with l ls 1, \
     ''                   using 1:($5/1024) title 'Buffers' with l ls 2, \
     ''                   using 1:($6/1024) title 'Cached' with l ls 3, \
     ''                   using 1:($3/1024-$5/1024-$6/1024) title 'Used' with l ls 4
