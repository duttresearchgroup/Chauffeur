#!/usr/bin/gnuplot
reset

set terminal pngcairo enhanced font 'Verdana,8'
set output '../tmp_graphs/cpu.png'
set title "CPU usage graph"
set key bmargin

set style line 1 lc rgb '#e74c3c' pt 1 ps 1 lt 1 lw 2 
set style line 2 lc rgb '#3498db' pt 6 ps 1 lt 1 lw 2 
set style line 3 lc rgb '#28b463' pt 4 ps 1 lt 1 lw 2 

set border 3 back ls 11
set tics nomirror
set autoscale xy
set xdata time
set timefmt "%H:%M:%S"
set format x "%H:%M"
set xlabel "Time"
set ylabel "CPU %"

set style line 11 lc rgb '#aeb6bf' lt 0 lw 2
set grid back ls 11

plot '../tmp_data/cpu.dat' using 1:3 title 'User%' with l ls 1, \
     ''                   using 1:5 title 'System%' with l ls 2, \
     ''                   using 1:8 title 'Idle%' with l ls 3
